== Creating a new gradle plugin

=== Sources

*build.gradle*
[source,groovy]
----
plugins {
  id 'groovy'
  id 'maven-publish'
  id 'java-gradle-plugin'
}

group 'com.github.apycazo.plugins'
version '1.0.0-SNAPSHOT'

gradlePlugin {
  plugins {
    simplePlugin {
      id = 'com.github.apycazo.plugins.demoPlugin' // <1>
      implementationClass = 'com.github.apycazo.plugins.DemoPlugin' // <2>
    }
  }
}
----
<1> This is the plugin id, which we need to use later.
<2> The implementation class, which does not need to match the plugin name.

*implementation*
[source,java]
----
package com.github.apycazo.plugins

import org.gradle.api.Plugin
import org.gradle.api.Project
import org.gradle.api.tasks.compile.JavaCompile
import org.gradle.api.tasks.testing.Test
import org.gradle.api.publish.maven.MavenPublication

class DemoPlugin implements Plugin<Project> {

  @Override
  void apply(Project project) {
    project.apply plugin: 'java-library'
    project.apply plugin: 'maven-publish'

    // CI number from jenkins, where available
    def buildNumber = System.getenv('BUILD_NUMBER') ?: 'LOCAL'
    // Branch name, fixed for jenkins.
    def gitCmd = 'git rev-parse --abbrev-ref HEAD';
    def branch = System.getenv("BRANCH_NAME") ?: gitCmd.execute().getText()

    project.tasks.withType(JavaCompile) {
      options.encoding = 'UTF-8'
    }

    project.tasks.withType(Test) {
      doFirst {
        testLogging {
          events "passed", "skipped", "failed", "standardOut", "standardError"
        }
        def basePath = project.rootDir.getPath()
        systemProperties = [
          "CONFIG_PATH": System.getProperty("CONFIG_PATH", "$basePath/cfg")
        ]
      }
    }

    project.clean.doFirst {
      println "Deleting $project.projectDir/out"
      delete "$project.projectDir/out"
      delete "$project.projectDir/target"
    }

    project.jar {
      manifest {
        attributes(
          'Build': new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
          'Implementation-Version': project.version,
          'Implementation-Title': project.name,
          'Vendor': 'apycazo',
          'CI-build': buildNumber,
          'Last-commit': 'git rev-parse HEAD'.execute().getText().trim(),
          'Branch': branch,
          'Dependencies': project.configurations.runtimeClasspath.allDependencies
            .collect {"${it.group}:${it.name}:${it.version}"}).join(",")
      }
    }

    project.publishing {
      publications {
        mavenJava(MavenPublication) {
          from project.components.java
        }
      }
    }
  }
}
----

=== Usage

[source,groovy]
----
plugins {
  id 'com.github.apycazo.plugins.demoPlugin' version '1.0.0-SNAPSHOT'
}
----

=== Include maven local as repository for plugins

*Option 1: settings.gradle*
[source,groovy]
----
pluginManagement {
  repositories {
    mavenLocal()
    gradlePluginPortal()
  }
}
----

*Option 2: Specific apply*
[source,groovy]
----
buildscript {
  repositories {
    mavenLocal()
  }
  dependencies {
    classpath 'com.github.apycazo.plugins:demoPlugin:1.0.0-SNAPSHOT'
  }
}

apply plugin: 'com.github.apycazo.plugins.demoPlugin'
----
